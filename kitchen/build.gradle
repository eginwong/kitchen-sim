plugins {
    id "com.google.protobuf" version "0.8.8"
    id "java"
    id "idea"
    id "application"
}

group 'com.eginwong.kitchensim'
version '1.0-SNAPSHOT'

def grpcVersion = '1.18.0'
def protobufVersion = '3.6.1'

repositories {
    mavenCentral()
}

dependencies {

    implementation "io.grpc:grpc-core:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    compileOnly "javax.annotation:javax.annotation-api:1.2"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:1.9.5"
}

mainClassName = 'com.eginwong.kitchensim.KitchenServer'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}
